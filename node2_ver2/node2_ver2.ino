#include <Arduino.h>
#include <ModbusRTU.h>
#include <AccelStepper.h>
#include "HX711.h"

// Slave address
#define SLAVE_ID 0x12
#define AQUA_ID  0x01

// UART2 for RS485
#define UART2_TX 17
#define UART2_RX 16

// Buttons
#define UPBTN    18
#define DOWNBTN  21
#define STOPBTN  19
#define RESETBTN 13

unsigned long lastBtnTime = 0;
const unsigned long debounceMs = 120;

// È¶¨ÈÅî
#define STEP_PIN 14
#define DIR_PIN  12
AccelStepper stepper(AccelStepper::DRIVER, STEP_PIN, DIR_PIN);

enum WinchState {
  STOPPED,
  RUN_UP,
  RUN_DOWN
};

WinchState winchState = STOPPED;

// Áß§ÈáçÊÑüÊ∏¨Âô®
#define HX711_DT  26
#define HX711_SCK 27
HX711 scale;

int32_t currentTension = 0;
int32_t tensionThreshold = 0;

// Modbus
HardwareSerial MainBus(2);
ModbusRTU mb;

// Êö´Â≠òÂô®ÂÆöÁæ©
enum {
  HR_SPEED = 0,       // 40000: MaxSpeed
  HR_ACC,             // 40001: Acceleration
  HR_THR_H,           // 40002: tensionThreshold È´ò16‰Ωç
  HR_THR_L,           // 40003: tensionThreshold ‰Ωé16‰Ωç
  HR_MOV_H,           // 40004: moveTo step È´ò16‰Ωç
  HR_MOV_L,           // 40005: moveTo step ‰Ωé16‰Ωç
  HR_CURPOS_H,        // 40006: set current pos È´ò16‰Ωç
  HR_CURPOS_L,        // 40007: set current pos ‰Ωé16‰Ωç
  HR_COUNT
  
};



enum {
  IR_TEN_H = 6,       // 30006: currentTension È´ò16‰Ωç
  IR_TEN_L,           // 30007: currentTension ‰Ωé16‰Ωç
  IR_POS_H,           // 30008: currentPosition È´ò16‰Ωç
  IR_POS_L,           // 30009: currentPosition ‰Ωé16‰Ωç
  IR_RUN_STATE,      // 30010: Motor Run State (0xFF=running, 0x00=stopped)
};

enum {
  COIL_RUN = 0,       // Coil 0: Motor Run/Stop
  COIL_COUNT
};

static inline int32_t combineToInt32(uint16_t high, uint16_t low){
  uint32_t u = ((uint32_t)high << 16) | (uint32_t)low;
  return (int32_t)u;
}


// callback functions
uint16_t cbSetCurPos(TRegister* reg, uint16_t val) {
  uint16_t high = mb.Hreg(HR_CURPOS_H);
  uint16_t low  = val;
  int32_t newPos = combineToInt32(high, low);
  stepper.setCurrentPosition(newPos);
  Serial.printf("[CMD] Master set CurrentStep = %ld\n", newPos);
  return val;
}

// Coil ÊéßÂà∂ È¶¨ÈÅîÁ∑äÊÄ•ÁÖûËªä
uint16_t cbMotorRun(TRegister* reg, uint16_t val) {
  if(!COIL_BOOL(val)){
    stepper.setCurrentPosition(stepper.currentPosition());
    Serial.println("[CMD] Motor STOP");
  }
  return val;

}


uint16_t cbSetTargetPos(TRegister* reg, uint16_t val) {
  uint16_t high = mb.Hreg(HR_MOV_H);
  uint16_t low  = val;
  int32_t newStep = combineToInt32(high, low);
  stepper.moveTo(newStep);
  Serial.printf("[CMD] MoveTo step=%ld\n", newStep);
  return val;
}

uint16_t cbSetSpeed(TRegister* reg, uint16_t val) {
  stepper.setMaxSpeed(val);
  Serial.printf("[CMD] Set speed=%d\n", val);
  return val;
}

uint16_t cbSetAcc(TRegister* reg, uint16_t val) {
  stepper.setAcceleration(val);
  Serial.printf("[CMD] Set acc=%d\n", val);
  return val;
}

uint16_t cbSetThr(TRegister* reg, uint16_t val) {
  uint16_t high = mb.Hreg(HR_THR_H);
  uint16_t low  = val;
  tensionThreshold = combineToInt32(high, low);
  Serial.printf("[CMD] Set tensionThreshold=%d\n", tensionThreshold);
  return val;
}

// Node0 task
void node0Task(void *pv) {
  (void)pv;
  for (;;) {
    mb.task();
    vTaskDelay(10 / portTICK_PERIOD_MS); // ËÆìÂá∫ CPU
  }
}



// =================== Aqua task ===========================
//UART
#define UART1_TX 23
#define UART1_RX 22
HardwareSerial AquaBus(1);

#define AQUA_TIMEOUT 6000
#define AQUA_SENSOR_COUNT 21     

const uint8_t aquaWakeupCmd[4] = {0x01, 0x11, 0xC0, 0x2C};
const uint8_t specialRegisterCmd[8] = {0x01, 0x03, 0x25, 0x23, 0x00, 0x01, 0x7E, 0xCC};
uint8_t aquaSpecialCmd[11] = {0x01, 0x10, 0x25, 0x24, 0x00, 0x01, 0x02, 0x07, 0xD0, 0xD7, 0xDA};

bool aqua_waked = false;

volatile int currentSensor = 0;

// Êö´Â≠òÂô®ÂÆöÁæ©
enum {
  IR_AQ_TEMP_H = 20, // 40008: Aqua Ê∫´Â∫¶ È´ò16‰Ωç
  IR_AQ_TEMP_L,     // 40009: Aqua Ê∫´Â∫¶ ‰Ωé16‰Ωç
  IR_AQ_PRES_H,     // 40010: Aqua Â£ìÂäõ È´ò16‰Ωç
  IR_AQ_PRES_L,     // 40011: Aqua Â£ìÂäõ ‰Ωé16‰Ωç
  IR_AQ_DEPTH_H,    // 40012: Aqua Ê∑±Â∫¶ È´ò16‰Ωç
  IR_AQ_DEPTH_L,    // 40013: Aqua Ê∑±Â∫¶ ‰Ωé16
  IR_AQ_LEVEL_H,    // 40014: Aqua Ê∞¥‰Ωç È´ò16‰Ωç
  IR_AQ_LEVEL_L,    // 40015: Aqua Ê∞¥‰Ωç ‰Ωé16
  IR_AQ_ALCON_H,    // 40016: Aqua ÂØ¶ÈöõÂ∞éÈõªÁéá È´ò16‰Ωç
  IR_AQ_ALCON_L,    // 40017: Aqua ÂØ¶ÈöõÂ∞éÈõªÁéá ‰Ωé16‰Ωç
  IR_AQ_SCON_H,    // 40018: Aqua ÁâπÂÆöÂ∞éÈõªÁéá È´ò16‰Ωç
  IR_AQ_SCON_L,    // 40019: Aqua ÁâπÂÆöÂ∞éÈõªÁéá ‰Ωé16‰Ωç
  IR_AQ_RESIS_H,   // 40020: Aqua ÈõªÈòªÁéá È´ò16‰Ωç
  IR_AQ_RESIS_L,   // 40021: Aqua ÈõªÈòªÁéá ‰Ωé16‰Ωç
  IR_AQ_SALIN_H,   // 40022: Aqua ÈπΩÂ∫¶ È´ò16‰Ωç
  
};

// Aqua ÂêÑÊÑüÊ∏¨Âô®ÂØÑÂ≠òÂô®ËÆÄÂèñÊåá‰ª§
const uint8_t aquaCmds[AQUA_SENSOR_COUNT][8] = {
    {0x01,0x03,0x15,0x4A,0x00,0x07,0x21,0xD2}, // 0 Ê∫´Â∫¶
    {0x01,0x03,0x15,0x51,0x00,0x07,0x51,0xD5}, // 1 Â£ìÂäõ
    {0x01,0x03,0x15,0x58,0x00,0x07,0x81,0xD7}, // 2 Ê∑±Â∫¶
    {0x01,0x03,0x15,0x5F,0x00,0x07,0x30,0x16}, // 3 Ê∞¥‰Ωç
    {0x01,0x03,0x15,0x66,0x00,0x07,0xE0,0x1B}, // 4 Ë°®Èù¢È´òÁ®ã
    {0x01,0x03,0x15,0x82,0x00,0x07,0xA0,0x2C}, // 5 ÂØ¶ÈöõÂ∞éÈõªÁéá
    {0x01,0x03,0x15,0x89,0x00,0x07,0xD1,0xEE}, // 6 ÁâπÂÆöÂ∞éÈõªÁéá
    {0x01,0x03,0x15,0x90,0x00,0x07,0x00,0x29}, // 7 ÈõªÈòªÁéá
    {0x01,0x03,0x15,0x97,0x00,0x07,0xB1,0xE8}, // 8 ÈπΩÂ∫¶
    {0x01,0x03,0x15,0x9E,0x00,0x07,0x61,0xEA}, // 9 Á∏ΩÊ∫∂Ëß£Âõ∫È´î
    {0x01,0x03,0x15,0xA5,0x00,0x07,0x10,0x27}, // 10 Ê∞¥ÂØÜÂ∫¶
    {0x01,0x03,0x15,0xB3,0x00,0x07,0xF1,0xE3}, // 11 Â§ßÊ∞£Â£ìÂäõ
    {0x01,0x03,0x15,0xBA,0x00,0x07,0x21,0xE1}, // 12 pHÂÄº
    {0x01,0x03,0x15,0xC1,0x00,0x07,0x51,0xF8}, // 13 pHÊØ´‰ºè
    {0x01,0x03,0x15,0xC8,0x00,0x07,0x81,0xFA}, // 14 ORP
    {0x01,0x03,0x15,0xCF,0x00,0x07,0x30,0x3B}, // 15 Ê∫∂Ëß£Ê∞ßÊøÉÂ∫¶
    {0x01,0x03,0x15,0xD6,0x00,0x07,0xE1,0xFC}, // 16 Ê∫∂Ëß£Ê∞ßÈ£ΩÂíåÂ∫¶
    {0x01,0x03,0x15,0xF2,0x00,0x07,0xA1,0xF7}, // 17 ÊøÅÂ∫¶
    {0x01,0x03,0x16,0x15,0x00,0x07,0x11,0x84}, // 18 Ê∞ßÂàÜÂ£ì
    {0x01,0x03,0x16,0x23,0x00,0x07,0xF1,0x8A}, // 19 Â§ñÈÉ®ÈõªÂ£ì
    {0x01,0x03,0x16,0x2A,0x00,0x07,0x21,0x88}  // 20 ÈõªÊ±†ÂÆπÈáè
};

uint8_t AquaIregOffset = 20;
float AquaInputBuff[AQUA_SENSOR_COUNT];
// Protect AquaInputBuff with mutex
SemaphoreHandle_t aquaMutex = NULL;

uint16_t crc16(const uint8_t *data, uint16_t len) {
    uint16_t crc = 0xFFFF;
    for (uint16_t i = 0; i < len; i++) {
        crc ^= data[i];
        for (uint8_t j = 0; j < 8; j++) {
            if (crc & 0x0001) crc = (crc >> 1) ^ 0xA001;
            else crc = crc >> 1;
        }
    }
    return crc;
}

void wakeupAqua() {

  uint8_t buffer[64];
  int rlen = 0;
  unsigned long start = millis();

  Serial.println("üîÑ Sending wakeup command repeatedly...");

  // Step 1: Keep sending wakeup command until any response is received
  while (true) {
    AquaBus.write(aquaWakeupCmd, sizeof(aquaWakeupCmd));

    start = millis();
    bool gotResponse = false;

    // Wait up to 1000ms for any response
    while (millis() - start < 1000) {
      if (AquaBus.available() > 0) {
        while (AquaBus.available()){
          if (rlen < sizeof(buffer)) {
            buffer[rlen++] = AquaBus.read();
          }
        }
        gotResponse = true;
        break;
      }
    }

    if (gotResponse) {
      Serial.println("‚úÖ Aqua responded. Proceeding to special command...");
      delay(200); // Optional delay between retries to avoid spamming
      // Ë∑≥ÈÅé special code!!!
      while (AquaBus.available()) {
        AquaBus.read(); // ‰∏üÊéâ‰∏ä‰∏ÄÊ¨°ÁöÑË≥áÊñô
      }
      aqua_waked = true;
      return;
    }
  }
}





float bytesToFloat(uint8_t* bytes) {
  uint8_t tmp[4];
  tmp[0] = bytes[3];
  tmp[1] = bytes[2];
  tmp[2] = bytes[1];
  tmp[3] = bytes[0];
  float f;
  memcpy(&f, tmp, sizeof(f));
  return f;
}

bool readAquaRegister(size_t index, float &value) {
  if (index >= AQUA_SENSOR_COUNT) return false;

  const uint8_t *cmd = aquaCmds[index];
  AquaBus.write(cmd, 8);
  unsigned long start = millis();
  uint8_t resp[19];
  int rlen = 0;

  while (millis() - start < AQUA_TIMEOUT) {
    if (AquaBus.available()) {
      resp[rlen++] = AquaBus.read();
      if (rlen == 19) break;
    }
  }
  if (rlen != 19) {
    Serial.println("[AquaTask] Aqua timeout / response error");
    return false;
  }
  value = bytesToFloat(&resp[3]);
  if (xSemaphoreTake(aquaMutex, (TickType_t)10 / portTICK_PERIOD_MS) == pdTRUE) {
    AquaInputBuff[currentSensor] = value;
    xSemaphoreGive(aquaMutex);
  } else {
    // fallback: still set volatile (not ideal) but we try
    AquaInputBuff[currentSensor] = value;
  }
  return true;
}

// ------------------- Task -------------------
void aquaTask(void *pv) {
  
  Serial.println("[AquaTask] Sending wakeup command repeatedly...");
  if(!aqua_waked){
    wakeupAqua();
  }
  unsigned long start = millis();
  for (;;) {
    float value = 0.0;
    if (readAquaRegister(currentSensor, value)) {
      Serial.print("[AquaTask] Sensor Index ");
      Serial.print(currentSensor);
      Serial.print(" ‚Üí Value: ");
      Serial.println(value, 4); // 4 decimal places
    } else {
      Serial.print("[AquaTask] Sensor Index ");
      Serial.print(currentSensor);
      Serial.println(" ‚Üí ‚ùå Failed to read");
    }
    currentSensor = (currentSensor + 1) % AQUA_SENSOR_COUNT;
    //delay(100); // wait before next sensor read
    if(currentSensor == 0){
      unsigned long operation_time = millis()-start;
      start = millis();
      Serial.print("[AquaTask] Loop operation time: ");
      Serial.print(operation_time);
      Serial.println(" ms");
      // print aqua input buffer raw data in hex
      Serial.println("Aqua Input Buffer (Hex):");
      if (xSemaphoreTake(aquaMutex, (TickType_t)10 / portTICK_PERIOD_MS) == pdTRUE){ 
        for(int i=0;i<AQUA_SENSOR_COUNT;i++){
          Serial.printf("%f ", AquaInputBuff[i]);
          if((i+1)%8==0) Serial.println();
          vTaskDelay(10 / portTICK_PERIOD_MS); // ËÆìÂá∫ CPU
        }
        xSemaphoreGive(aquaMutex);
      }
      
    }
    
  }
    

}
  

void setup() {
  Serial.begin(115200);

  // ÂàùÂßãÂåñ Mainbus
  MainBus.begin(19200, SERIAL_8E1, UART2_RX, UART2_TX);
  mb.begin(&MainBus);
  mb.slave(SLAVE_ID);

  // ÂàùÂßãÂåñ AquaBus
  AquaBus.begin(19200, SERIAL_8E1, UART1_RX, UART1_TX);

  // Create mutex for aqua buffer
  aquaMutex = xSemaphoreCreateMutex();
  if (aquaMutex == NULL) {
    Serial.println("‚ö†Ô∏è Failed to create aqua mutex");
  }

  // ÈÖçÁΩÆÊö´Â≠òÂô®
  mb.addHreg(HR_SPEED, 2000);
  mb.addHreg(HR_ACC, 1000);
  mb.addHreg(HR_THR_H, 0);
  mb.addHreg(HR_THR_L, 0);
  mb.addHreg(HR_MOV_H, 0);
  mb.addHreg(HR_MOV_L, 0);
  mb.addHreg(HR_CURPOS_H, 0);
  mb.addHreg(HR_CURPOS_L, 0);

  mb.addIreg(IR_TEN_H, 0);
  mb.addIreg(IR_TEN_L, 0);
  mb.addIreg(IR_POS_H, 0);
  mb.addIreg(IR_POS_L, 0);
  mb.addIreg(IR_RUN_STATE, 0);
  // Êñ∞Â¢û Aqua ÊÑüÊ∏¨Âô®Êö´Â≠òÂô®, offset 20, ÊØèÂÄãsensorÊúâÂÖ©ÂÄãreg, ÂàÜÂà•ÁÇ∫È´ò16‰ΩçÂíå‰Ωé16‰Ωç
  for(int i=0;i<AQUA_SENSOR_COUNT;i++){
    //È´ò‰Ωç
    mb.addIreg(AquaIregOffset + i*2, 0);
    //‰Ωé‰Ωç
    mb.addIreg(AquaIregOffset + i*2 + 1, 0);
  }

  mb.addCoil(COIL_RUN, true);

  // Áï∂ master ÂØ´ Hreg ÁöÑÂÄºÊôÇÔºåËß∏Áôº cbSetCurPos
  mb.onSetHreg(HR_CURPOS_L, cbSetCurPos);
  mb.onSetHreg(HR_MOV_L, cbSetTargetPos);
  mb.onSetCoil(COIL_RUN, cbMotorRun);
  mb.onSetHreg(HR_SPEED, cbSetSpeed);
  mb.onSetHreg(HR_ACC, cbSetAcc);
  mb.onSetHreg(HR_THR_L, cbSetThr);

  // Buttons
  pinMode(UPBTN, INPUT_PULLUP);
  pinMode(DOWNBTN, INPUT_PULLUP);
  pinMode(STOPBTN, INPUT_PULLUP);
  pinMode(RESETBTN, INPUT_PULLUP);

  // È¶¨ÈÅîÂàùÂßãÂåñ
  stepper.setMaxSpeed(2000);
  stepper.setAcceleration(1000);

  // HX711
  scale.begin(HX711_DT, HX711_SCK);
  Serial.println("HX711 ready");
  xTaskCreatePinnedToCore(node0Task, "node0Task", 4096, NULL, 1, NULL, 0);
  xTaskCreatePinnedToCore(aquaTask, "aquaTask", 4096, NULL, 1, NULL, 1);
  Serial.println("Aqua Setup done");

}


void handleButtons() {
  unsigned long now = millis();
  if (now - lastBtnTime < debounceMs) return;
  if (digitalRead(UPBTN) == LOW) {   // Êåâ‰∏ã UP
    lastBtnTime = now;
    if(winchState != WinchState::RUN_UP){
      if(winchState == WinchState::RUN_DOWN){
        stepper.setCurrentPosition(stepper.currentPosition());
        Serial.println("[BUTTON] Motor STOP");
      }
      Serial.println("[BUTTON] Motor run UP");
      stepper.move(1000000);
    }
  } else if (digitalRead(DOWNBTN) == LOW) {  // Êåâ‰∏ã DOWN
    lastBtnTime = now;
    if(winchState != WinchState::RUN_DOWN){
      if(winchState == WinchState::RUN_UP){
        stepper.setCurrentPosition(stepper.currentPosition());
        Serial.println("[BUTTON] Motor STOP");
      }
      Serial.println("[BUTTON] Motor run DOWN");
      stepper.move(-1000000);
    }
  } else if (digitalRead(STOPBTN) == LOW) {  // Êåâ‰∏ã STOP
    lastBtnTime = now;
    if(winchState != WinchState::STOPPED){
      stepper.setCurrentPosition(stepper.currentPosition()); // Á°¨ÂÅú
      Serial.println("[BUTTON] Motor STOP");
    }
  } else if (digitalRead(RESETBTN) == LOW) { // Reset
    lastBtnTime = now;
    if(stepper.currentPosition()!=0){
      Serial.println("[BUTTON] RESET position to 0");
      stepper.setCurrentPosition(0);
    }
  }
}

void loop() {
  handleButtons();

  // Êõ¥Êñ∞ÊÑüÊ∏¨Êï∏Êìö
  if (scale.is_ready()) {
    currentTension = (int32_t)scale.read();
  }

  if(stepper.distanceToGo()!=0){
    if(stepper.targetPosition()>stepper.currentPosition()){
      winchState = WinchState::RUN_UP;
    } else {
      winchState = WinchState::RUN_DOWN;
    }
  } else {
    winchState = WinchState::STOPPED;
  }
  //Â¶ÇÊûú tension<tension thresholdÔºå‰∏çÂèØ‰∏ãÈôç
  if(winchState == WinchState::RUN_DOWN && currentTension<tensionThreshold){
    stepper.setCurrentPosition(stepper.currentPosition());
    Serial.println("[SAFETY] Tension below threshold, Motor STOP");
  }

  // Êõ¥Êñ∞ Input Register
  uint32_t tens_raw = (uint32_t)currentTension;
  mb.Ireg(IR_TEN_H, (uint16_t)((tens_raw >> 16) & 0xFFFF));
  mb.Ireg(IR_TEN_L, (uint16_t)(tens_raw & 0xFFFF));
  int32_t pos = stepper.currentPosition();
  uint32_t pos_raw = (uint32_t)pos;
  mb.Ireg(IR_POS_H, (uint16_t)((pos_raw >> 16) & 0xFFFF));
  mb.Ireg(IR_POS_L, (uint16_t)(pos_raw & 0xFFFF));
  mb.Ireg(IR_RUN_STATE, (stepper.distanceToGo() != 0) ? 0xFF : 0x00);


  // Êõ¥Êñ∞ÊâÄÊúâÁöÑ Aqua ÊÑüÊ∏¨Âô®Êö´Â≠òÂô® from float buffer using safe memcpy
  if (xSemaphoreTake(aquaMutex, (TickType_t)5 / portTICK_PERIOD_MS) == pdTRUE) {
    for (int i = 0; i < AQUA_SENSOR_COUNT; i++) {
      uint32_t raw;
      memcpy(&raw, &AquaInputBuff[i], sizeof(raw)); // reinterpret float bits as uint32
      mb.Ireg(AquaIregOffset + i * 2, (uint16_t)((raw >> 16) & 0xFFFF));
      mb.Ireg(AquaIregOffset + i * 2 + 1, (uint16_t)(raw & 0xFFFF));
    }
    xSemaphoreGive(aquaMutex);
  } else {
    //Serial.println("‚ö†Ô∏è Aqua mutex busy, skipping update");
    // If mutex busy, skip updating this cycle (to avoid blocking)
  }

  
  stepper.run();
  vTaskDelay(1 / portTICK_PERIOD_MS);
}
